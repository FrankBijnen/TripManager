; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "TripManager"
#define MyAppVersion GetVersionNumbersString('..\Win32\TripManager.exe')
#define MyAppPublisher "TDBware"
#define ExeName "TripManager.exe"
#define DefaultDirName "{code:DefaultDir}\TripManager"
#define MyAppId "{{33018F95-765A-49CA-B6FC-0B3AB06701BD}"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={#MyAppId}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={#DefaultDirName}
DisableDirPage=no
DefaultGroupName={#MyAppName}
UninstallDisplayIcon={app}\{#ExeName}
OutputDir=.
OutputBaseFilename=setup_{#MyAppName}
Compression=lzma2/normal
LZMANumBlockThreads=16
LZMAUseSeparateProcess=yes
SolidCompression=no
RestartIfNeededByRun=no
ArchitecturesInstallIn64BitMode=x64compatible

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Components]
Name: ExecutableWin32;                  Description: "Install Executable (Win32)";                  types: full;          Check: Win32;
Name: ExecutableWin64;                  Description: "Install Executable (Win64)";                  types: full;          Check: Win64;
Name: Symbols;                          Description: "Install Symbols";                             types: full;
Name: Docs;                             Description: "Install Documentation";                       types: full;
;
Name: Trk2RT;                           Description: "Install Trk2Rt (Contributed by S.M. Follen) Installs VC Runtime if needed!";  \
                                                                                                    types: full;

[Tasks]
Name: "desktopicon";                    Description: "{cm:CreateDesktopIcon}";                GroupDescription: "{cm:AdditionalIcons}";

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "..\Win32\TripManager.exe";       DestDir: "{app}"; Components: ExecutableWin32;      Flags: ignoreversion
Source: "..\Win32\Trk2Fit.exe";           DestDir: "{app}"; Components: ExecutableWin32;      Flags: ignoreversion
Source: "..\Win32\Fit2Gpx.exe";           DestDir: "{app}"; Components: ExecutableWin32;      Flags: ignoreversion
Source: "..\Win32\FitInfo.exe";           DestDir: "{app}"; Components: ExecutableWin32;      Flags: ignoreversion
Source: "..\Win32\WebView2Loader.dll";    DestDir: "{app}"; Components: ExecutableWin32;      Flags: ignoreversion
Source: "..\Win32\sqlite3.dll";           DestDir: "{app}"; Components: ExecutableWin32;      Flags: ignoreversion

Source: "..\Win64\TripManager.exe";       DestDir: "{app}"; Components: ExecutableWin64;      Flags: ignoreversion
Source: "..\Win64\Trk2Fit.exe";           DestDir: "{app}"; Components: ExecutableWin64;      Flags: ignoreversion
Source: "..\Win64\Fit2Gpx.exe";           DestDir: "{app}"; Components: ExecutableWin64;      Flags: ignoreversion
Source: "..\Win64\FitInfo.exe";           DestDir: "{app}"; Components: ExecutableWin64;      Flags: ignoreversion
Source: "..\Win64\WebView2Loader.dll";    DestDir: "{app}"; Components: ExecutableWin64;      Flags: ignoreversion
Source: "..\Win64\sqlite3.dll";           DestDir: "{app}"; Components: ExecutableWin64;      Flags: ignoreversion

; Images
Source: "..\Win32\Symbols\*";             DestDir: "{app}\Symbols";     Components: Symbols;  Flags: ignoreversion recursesubdirs; Excludes: "*.png,*.jbf,*.db"

; Docs
Source: "..\docs\tripmanager.chm";        DestDir: "{app}\ChmDocs";     Components: Docs;     Flags: ignoreversion;

; Trk2Rt
Source: "Trk2Rt\Trk2Rt.exe";              DestDir: "{app}";             Components: Trk2RT;   Flags: ignoreversion;
Source: "Trk2Rt\vc_redist.x86.exe";       DestDir: "{tmp}";             Components: Trk2RT;   Flags: ignoreversion; Check: VC_RTL_IsNeeded(false);

[Registry]
// .GPX associations
Root: HKCR; Subkey: "SystemFileAssociations\.gpx"; \
            Flags: uninsdeletekey; Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell"; \
            Flags: uninsdeletekey; Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction"; \
            Flags: uninsdeletekey; Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction"; \
            ValueName: "MUIVerb"; ValueType: string; ValueData: "Trk2RT"; \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction"; \
            ValueName: "subcommands"; ValueType: string; ValueData: ""; \
            Flags: uninsdeletekey;  Components: Trk2RT;

Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction\shell";  \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction\shell\a_routefromtrack"; \
            ValueType: string; ValueData: "Create route from track"; \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction\shell\a_routefromtrack\command"; \
            ValueType: string; ValueData: """{app}\Trk2Rt.exe"" exportPath=""%L"" ""%L""";  \
            Flags: uninsdeletekey;  Components: Trk2RT;

Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction\shell"; \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction\shell\b_openintrk2rt"; \
            ValueType: string; ValueData: "Open in TRk2RT"; \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.gpx\shell\Trk2RTAction\shell\b_openintrk2rt\command"; \
            ValueType: string; ValueData: """{app}\Trk2Rt.exe"" ""%L""";  \
            Flags: uninsdeletekey;  Components: Trk2RT;

// .KML associations
Root: HKCR; Subkey: "SystemFileAssociations\.kml"; \
            Flags: uninsdeletekey; Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell"; \
            Flags: uninsdeletekey; Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction"; \
            Flags: uninsdeletekey; Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction"; \
            ValueName: "MUIVerb"; ValueType: string; ValueData: "Trk2RT"; \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction"; \
            ValueName: "subcommands"; ValueType: string; ValueData: ""; \
            Flags: uninsdeletekey;  Components: Trk2RT;

Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction\shell";  \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction\shell\a_routefromtrack"; \
            ValueType: string; ValueData: "Create route from track"; \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction\shell\a_routefromtrack\command"; \
            ValueType: string; ValueData: """{app}\Trk2Rt.exe"" exportPath=""%L"" ""%L""";  \
            Flags: uninsdeletekey;  Components: Trk2RT;

Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction\shell"; \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction\shell\b_openintrk2rt"; \
            ValueType: string; ValueData: "Open in TRk2RT"; \
            Flags: uninsdeletekey;  Components: Trk2RT;
Root: HKCR; Subkey: "SystemFileAssociations\.kml\shell\Trk2RTAction\shell\b_openintrk2rt\command"; \
            ValueType: string; ValueData: """{app}\Trk2Rt.exe"" ""%L""";  \
            Flags: uninsdeletekey;  Components: Trk2RT;

[InstallDelete]

[Icons]
Name: "{group}\{#MyAppName}";         Filename: "{app}\TripManager.exe"
Name: "{autodesktop}\{#MyAppName}";   Filename: "{app}\TripManager.exe";              tasks: desktopicon;

[Run]
Filename: "{tmp}\vc_redist.x86.exe";  Parameters: "/passive /norestart";  \
            Components: Trk2RT; Flags: runascurrentuser skipifdoesntexist

[Code]

{ @TLama's function from https://stackoverflow.com/q/14392921/850848 }
function CmdLineParamExists(const Value: string): Boolean;
var
  I: Integer;
begin
  Result := False;
  for I := 1 to ParamCount do
    if CompareText(ParamStr(I), Value) = 0 then
    begin
      Result := True;
      Exit;
    end;
end;

function Win32: boolean;
begin
  result := (not IsWin64) or CmdLineParamExists('/Win32');
end;

function Win64: boolean;
begin
  result := not Win32;
end;

function Never: boolean;
begin
  result := false;
end;

// check if the needed RTL 32/64 bits is installed (by looking the registry)

// minimal RTL version required (default: 14.23.27820.0)
// minimal VC2019 RTL version: 14.29.30133.0
// minimal VC2022 RTL version: 14.32.31326.0
const VCRTL_MIN_V1 = 14;
      VCRTL_MIN_V2 = 23;
      VCRTL_MIN_V3 = 27820;
      VCRTL_MIN_V4 = 0;

function VC_RTL_IsNeeded (bUse64BitsRTL: Boolean): Boolean;

var
  sRegKey: string;
  v1: Cardinal;
  v2: Cardinal;
  v3: Cardinal;
  v4: Cardinal;
begin
  // is it running on 64 bits OS?
  if IsWin64 then
  begin
    sRegKey := 'SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\';
    // need 64 bits RTL?
    if bUse64BitsRTL then
      sRegKey := sRegKey + 'x64'
    else
      sRegKey := sRegKey + 'x86'
  end
  // else we have only 32 bits RTL on 32 bits OS...
  else if not bUse64BitsRTL then
    sRegKey := 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\X86';
  // if we have a key, let's check it
  if ((sRegKey <> '') and
      RegQueryDWordValue (HKEY_LOCAL_MACHINE, sRegKey, 'Major', v1) and
      RegQueryDWordValue (HKEY_LOCAL_MACHINE, sRegKey, 'Minor', v2) and
      RegQueryDWordValue (HKEY_LOCAL_MACHINE, sRegKey, 'Bld',   v3) and
      RegQueryDWordValue (HKEY_LOCAL_MACHINE, sRegKey, 'RBld',  v4)) then
  begin
    Log ('VC 2015-2022 Redist version: ' + IntToStr (v1) +
        '.' + IntToStr (v2) + '.' + IntToStr (v3) +
        '.' + IntToStr (v4));
    { Version info was found. Return true if later or equal to our
       minimal required version RTL_MIN_Vx }
    Result := not (
        (v1 > VCRTL_MIN_V1) or ((v1 = VCRTL_MIN_V1) and
         ((v2 > VCRTL_MIN_V2) or ((v2 = VCRTL_MIN_V2) and
          ((v3 > VCRTL_MIN_V3) or ((v3 = VCRTL_MIN_V3) and
           (v4 >= VCRTL_MIN_V4)))))));
  end
  else
    Result := TRUE;
end;

function DefaultDir(Param: string): string;
begin
  result := ExpandConstant('{autopf}');
  if (IsWin64) and
     (CmdLineParamExists('/Win32')) then
    result := ExpandConstant('{autopf32}');
end;

procedure CopyRegKey(RegKey, OldRegName, NewRegName: string);
var
  RegNames: TArrayOfString;
  RegValue: string;
  Index: integer;
begin
  if RegKeyExists(HKCU, RegKey + NewRegName) then
    exit;
    
  if RegGetValueNames(HKCU, RegKey + OldRegName, RegNames) then
  begin
    for Index := 0 to High(RegNames) do
    begin
      if RegQueryStringValue(HKCU, RegKey + OldRegname, RegNames[Index], RegValue) then
      begin
        // Dont write default values
        if RegValue = 'Internal Storage\.System\Trips' then 
          continue;
        if RegValue = 'Internal Storage\GPX' then 
          continue;
        if RegValue = 'Internal Storage\POI' then 
          continue;
        if RegValue = '?:\Garmin\GPX' then 
          continue;
        if RegValue = '?:\Garmin\POI' then 
          continue;
        if RegValue = '?:\Garmin\Courses' then 
          continue;
        if RegValue = '?:\Garmin\NewFiles' then 
          continue;
        if RegValue = '?:\Garmin\Activities' then 
          continue;
          
        RegWriteStringValue(HKCU, RegKey + NewRegName, RegNames[Index], RegValue);
      end;  
    end;
  end;
end;

function InitializeSetup(): Boolean;
var
  RegKey: string;
  ErrorCode: integer;
  UninstallString: string;
begin
  Result := true;
  RegKey := 'SOFTWARE\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\' + ExpandConstant('{#MyAppId}') + '_is1';
  if RegQueryStringValue(HKLM, RegKey, 'UninstallString', UninstallString) and
     (Is64BitInstallMode) then
  begin
    result := MsgBox('The current TripManager version needs to be uninstalled before continuing. Uninstall now?' + #10 +
                     UninstallString, mbConfirmation, MB_YESNO) = IDYES;
    if (result) then
      ShellExec('', UninstallString, '/SILENT', '', SW_SHOWNORMAL, ewWaitUntilTerminated, ErrorCode);
  end;

  // Clean old registry keys
  RegKey := 'SOFTWARE\TDBware\' + ExpandConstant('{#MyAppName}');

  CopyRegKey(RegKey, '\0', '\zūmo XT');
  CopyRegKey(RegKey, '\1', '\zūmo XT2');
  CopyRegKey(RegKey, '\2', '\Tread 2');
  CopyRegKey(RegKey, '\3', '\Edge');
  CopyRegKey(RegKey, '\4', '\Garmin');
  
  RegDeleteValue(HKCU, RegKey, 'CurrentDevice');
  RegDeleteValue(HKCU, RegKey, 'ZumoModel');
  RegDeleteValue(HKCU, RegKey, 'EnableSendTo');
  RegDeleteValue(HKCU, RegKey, 'TripNameInList');
  RegDeleteValue(HKCU, RegKey, 'ExploreUuid');
  RegDeleteValue(HKCU, RegKey, 'ForceRecalc');
  RegDeleteValue(HKCU, RegKey, 'AddSubClasses');
  RegDeleteValue(HKCU, RegKey, 'PrefDeviceGpxFolder');
  RegDeleteValue(HKCU, RegKey, 'PrefDevicePoiFolder');
  
end;